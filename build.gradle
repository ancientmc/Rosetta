plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
}

group = 'com.ancientmc'
version = '0.1.3'

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/' }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'net.minecraftforge:srgutils:0.5.4'
    implementation 'org.ow2.asm:asm:9.7.1'
    implementation 'org.ow2.asm:asm-tree:9.7.1'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'net.minecraftforge:mapping-verifier:2.0.8:fatjar'
    testImplementation 'net.minecraftforge:srgutils:0.5.4'
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jar {
    archiveClassifier = 'base'
    from sourceSets.main.output
    manifest {
        attributes (
            'Specification-Title': 'Rosetta',
            'Specification-Ventor': 'AncientMC',
            'Specification-Version': archiveVersion,
            'Implementation-Title': 'com.ancientmc.rosetta',
            'Implementation-Vendor': 'AncientMC',
            'Implementation-Version': archiveVersion,
        )
    }
}

shadowJar {
    archiveClassifier = 'all'
    dependsOn jar
}

artifacts {
    archives shadowJar
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        group = project.group
        artifactId = 'rosetta'
        version = project.version
        pom {
            name = artifactId
            description = 'CLI program to generate and update TSRGv2 deobfuscation mappings between JAR versions.'
            url = 'https://github.com/ancientmc/rosetta'
            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://mit-license.org/'
                }
            }
            developers {
                developer {
                    id = 'moist-mason'
                    name = 'mason'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ancientmc-maven'
            url = '../ancientmc-maven/'
        }
    }
}
